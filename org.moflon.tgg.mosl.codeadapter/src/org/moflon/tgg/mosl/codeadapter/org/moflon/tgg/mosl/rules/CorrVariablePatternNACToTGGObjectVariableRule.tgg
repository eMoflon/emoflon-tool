#using org.moflon.tgg.mosl.codeadapter.*  #using AttrCondDefLibrary.*

#rule CorrVariablePatternNACToTGGObjectVariableRule #with org.moflon.tgg.mosl.codeadapter

#source {
	++ operator : Operator {
		value := "!"
	}
	++ corrVariablePattern : CorrVariablePattern {
		++   - op->operator
	}
	rule : Rule {
		++   - correspondencePatterns->corrVariablePattern
	}
}

#target {
	domainCorrespondence : Domain {
		type == enum::DomainType::CORRESPONDENCE
		name == "Correspondence"
	}
	tggRule : TGGRule {
		++   - objectVariable->tggObjectVariableCorr
		- correspondence->domainCorrespondence
	}
	++ tggObjectVariableCorr : TGGObjectVariable {
		bindingSemantics := enum::BindingSemantics::NEGATIVE
		bindingOperator := enum::BindingOperator::CHECK_ONLY
		++   - domain->domainCorrespondence
	}
}

#correspondence {
	++   corrVariablePatternToTggObjectVariableCorr : CorrVariablePatternToTGGObjectVariable {
		#src->corrVariablePattern
		#trg->tggObjectVariableCorr
	}
	ruleToTggRule : RuleToTGGRule {
		#src->rule
		#trg->tggRule
	}
}

#attributeConditions {
	eq (corrVariablePattern.name, tggObjectVariableCorr.name)
}
