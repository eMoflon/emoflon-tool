#using org.moflon.tgg.mosl.codeadapter.*  #using AttrCondDefLibrary.*

#rule AttributeVariableRule #with org.moflon.tgg.mosl.codeadapter

#source {
	objectVariablePattern : ObjectVariablePattern
	rule : Rule {
		- sourcePatterns->objectVariablePattern
		- attrConditions->attrCond
	}
	attrCond : AttrCond {
		++   - values->attributeVariable
	}
	++ attributeVariable : AttributeVariable {
		++   - objectVar->objectVariablePattern
	}
}

#target {
	tggRule : TGGRule {
		- objectVariable->tggObjectVariable
		- csp->tggCSP
	}
	tggCSP : CSP {
		- variables->attributeVariableNAC
		++   - variables->tggAttributeVariable
		- constraints->tggConstraint
	}
	tggConstraint : TGGConstraint {
		++   - variables->tggAttributeVariable
	}
	! attributeVariableNAC : AttributeVariable {
		objectVariable == objectVariablePattern . name
		attribute == attributeVariable . attribute
	}
	++ tggAttributeVariable : AttributeVariable
	tggObjectVariable : TGGObjectVariable
}

#correspondence {
	attrCondToTggConstraint : AttrCondToTGGConstraint {
		#src->attrCond
		#trg->tggConstraint
	}
	objectVariablePatternToTggObjectVariable : ObjectVariablePatternToTGGObjectVariable {
		#src->objectVariablePattern
		#trg->tggObjectVariable
	}
	ruleToTggRule : RuleToTGGRule {
		#src->rule
		#trg->tggRule
	}
	++   attributeVariableToTggAttributeVariable : ParamValueToVariable {
		#src->attributeVariable
		#trg->tggAttributeVariable
	}
}

#attributeConditions {
	eq (tggAttributeVariable.objectVariable, tggObjectVariable.name)
	eq (attributeVariable.attribute, tggAttributeVariable.attribute)
}

