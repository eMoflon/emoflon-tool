#using org.moflon.tgg.mosl.codeadapter.*  #using AttrCondDefLibrary.*

#rule AttributeVariableTargetOVRule #with org.moflon.tgg.mosl.codeadapter

#source {
	++ attributeVariable : AttributeVariable {
		++   - objectVar->objectVariablePattern
	}
	objectVariablePattern : ObjectVariablePattern
	rule : Rule {
		- targetPatterns->objectVariablePattern
		- attrConditions->attrCond
	}
	attrCond : AttrCond {
		++   - values->attributeVariable
	}
}

#target {
	++ tggAttributeVariable : AttributeVariable
	tggConstraint : TGGConstraint {
		++   - variables->tggAttributeVariable
	}
	tggRule : TGGRule {
		- objectVariable->tggObjectVariable
		- csp->tggCSP
	}
	tggObjectVariable : TGGObjectVariable
	! attributeVariableNAC : AttributeVariable {
		attribute == attributeVariable . attribute
		objectVariable == objectVariablePattern . name
	}
	tggCSP : CSP {
		- variables->attributeVariableNAC
		++   - variables->tggAttributeVariable
		- constraints->tggConstraint
	}
}

#correspondence {
	objectVariablePatternToTggObjectVariable : ObjectVariablePatternToTGGObjectVariable {
		#src->objectVariablePattern
		#trg->tggObjectVariable
	}
	++   attributeVariableToTggAttributeVariable : ParamValueToVariable {
		#src->attributeVariable
		#trg->tggAttributeVariable
	}
	attrCondToTggConstraint : AttrCondToTGGConstraint {
		#src->attrCond
		#trg->tggConstraint
	}
	ruleToTggRule : RuleToTGGRule {
		#src->rule
		#trg->tggRule
	}
}

#attributeConditions {
	eq (tggObjectVariable.name, tggAttributeVariable.objectVariable)
	eq (attributeVariable.attribute, tggAttributeVariable.attribute)
}
