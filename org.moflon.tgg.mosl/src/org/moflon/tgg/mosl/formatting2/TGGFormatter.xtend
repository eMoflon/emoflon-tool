/*
 * generated by Xtext
 */
package org.moflon.tgg.mosl.formatting2;

import com.google.inject.Inject
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.moflon.tgg.mosl.services.TGGGrammarAccess
import org.moflon.tgg.mosl.tgg.Adornment
import org.moflon.tgg.mosl.tgg.AttrCond
import org.moflon.tgg.mosl.tgg.AttrCondDef
import org.moflon.tgg.mosl.tgg.AttrCondDefLibrary
import org.moflon.tgg.mosl.tgg.AttributeAssignment
import org.moflon.tgg.mosl.tgg.AttributeConstraint
import org.moflon.tgg.mosl.tgg.CorrType
import org.moflon.tgg.mosl.tgg.CorrVariablePattern
import org.moflon.tgg.mosl.tgg.Import
import org.moflon.tgg.mosl.tgg.LinkVariablePattern
import org.moflon.tgg.mosl.tgg.LocalVariable
import org.moflon.tgg.mosl.tgg.ObjectVariablePattern
import org.moflon.tgg.mosl.tgg.Param
import org.moflon.tgg.mosl.tgg.ParamValue
import org.moflon.tgg.mosl.tgg.Rule
import org.moflon.tgg.mosl.tgg.Schema
import org.moflon.tgg.mosl.tgg.TripleGraphGrammarFile
import org.moflon.tgg.mosl.tgg.Using

import static org.moflon.tgg.mosl.tgg.TggPackage.Literals.*


class TGGFormatter extends AbstractFormatter2 {
	
	@Inject extension TGGGrammarAccess

	def dispatch void format(TripleGraphGrammarFile triplegraphgrammarfile, extension IFormattableDocument document) {
		format(triplegraphgrammarfile.getSchema(), document);
		for (Rule rules : triplegraphgrammarfile.getRules()) {
			format(rules, document);
		}
		format(triplegraphgrammarfile.library, document);
	}
	def dispatch void format(AttrCondDefLibrary library, extension IFormattableDocument document) {
		for (AttrCondDef attrCondDef : library.attributeCondDefs) {
			format(attrCondDef, document);
		}
	}

	def dispatch void format(Schema schema, extension IFormattableDocument document) {
		schema.append[newLines = 3]
		schema.regionFor.keyword("«schema»").prepend[setNewLines(2);highPriority]
		schema.regionFor.keyword("«source»").prepend[setNewLines(2)]
		schema.regionFor.keywordPairs("«source»", "}").get(0).interior[indent]
		schema.regionFor.keyword("«target»").prepend[setNewLines(2)]
		schema.regionFor.keywordPairs("«target»", "}").get(0).interior[indent]
		schema.regionFor.keyword("«correspondence»").prepend[setNewLines(2)]
		schema.regionFor.keyword("«attribute conditions»").prepend[setNewLines(2)]
		
		schema.regionFor.feature(SCHEMA__SOURCE_TYPES).surround[newLine]
		schema.regionFor.feature(SCHEMA__TARGET_TYPES).surround[newLine]
		
		for (Import imports : schema.getImports()) {
			imports.append[newLine]
		}
		for (Using using : schema.getUsing()) {
			using.append[newLine]
		}
		for (CorrType correspondenceTypes : schema.getCorrespondenceTypes()) {
			format(correspondenceTypes, document);
		}
		for (AttrCondDef attributeCondDefs : schema.getAttributeCondDefs()) {
			format(attributeCondDefs, document);
		}
	}

	def dispatch void format(Rule rule, extension IFormattableDocument document) {
		rule.append[newLines = 3]
		rule.regionFor.keyword("«abstract»").prepend[setNewLines(2);highPriority]
		if (!rule.abstractRule) {
			rule.regionFor.keyword("«rule»").prepend[setNewLines(2);highPriority]
		}
		rule.regionFor.keyword("«source»").prepend[setNewLines(2)]
		rule.regionFor.keyword("«target»").prepend[setNewLines(2)]
		rule.regionFor.keyword("«correspondence»").prepend[setNewLines(2)]
		rule.regionFor.keyword("«attribute conditions»").prepend[setNewLines(2)]
		rule.regionFor.keyword(",").prepend[noSpace]
		rule.regionFor.feature(RULE__KERNEL).append[noSpace]
		
		for (Import imports : rule.getImports()) {
			imports.append[newLine]
		}
		for (Using using : rule.getUsing()) {
			using.append[newLine]
		}
		for (ObjectVariablePattern sourcePatterns : rule.getSourcePatterns()) {
			format(sourcePatterns, document);
		}
		for (ObjectVariablePattern targetPatterns : rule.getTargetPatterns()) {
			format(targetPatterns, document);
		}
		for (CorrVariablePattern correspondencePatterns : rule.getCorrespondencePatterns()) {
			format(correspondencePatterns, document);
		}
		for (AttrCond attrConditions : rule.getAttrConditions()) {
			format(attrConditions, document);
		}
	}
	def dispatch void format(CorrType correspondenceType, extension IFormattableDocument document) {
		correspondenceType.surround[newLine]
		correspondenceType.surround[indent]
		correspondenceType.interior[indent]
		if(correspondenceType instanceof CorrType){
			correspondenceType.regionFor.keyword("-«src»->").prepend[newLine].append[noSpace]
			correspondenceType.regionFor.keyword("-«trg»->").prepend[newLine].append[noSpace]
			correspondenceType.regionFor.keyword("}").prepend[newLine]
		}
	}

	def dispatch void format(AttrCond attrcond, extension IFormattableDocument document) {
		attrcond.surround[newLine]
		attrcond.regionFor.feature(ATTR_COND__NAME).prepend[newLine]
		attrcond.surround[indent]
		var values = attrcond.getValues()
		for (ParamValue value : values) {
			if (values.indexOf(value) != 0) {
				if (value instanceof LocalVariable) {
					value.prepend[space = " ^"]
				} else {
					value.prepend[oneSpace]
				}
			} else {
				if (value instanceof LocalVariable) {
					value.prepend[space = "^"]
				} else {
					value.prepend[noSpace]
				}
			}
			value.append[noSpace]
			value.regionFor.keyword(".").surround[noSpace]
		}
	}

	def dispatch void format(AttrCondDef attrconddef, extension IFormattableDocument document) {
		attrconddef.surround[newLine]
		attrconddef.surround[indent]
		attrconddef.interior[indent]
		attrconddef.regionFor.keyword("(").surround[noSpace]
		attrconddef.regionFor.keyword("sync:").prepend[newLine]
		attrconddef.regionFor.keyword("gen:").prepend[newLine]
		attrconddef.regionFor.keyword("}").surround[newLine]
		for (Param params : attrconddef.getParams()) {
			params.regionFor.feature(PARAM__INDEX).append[noSpace]
			params.regionFor.feature(PARAM__TYPE).surround[noSpace]
		}
		for (Adornment adornment : attrconddef.getAllowedSyncAdornments()) {
			adornment.regionFor.feature(ADORNMENT__VALUE).append[noSpace]
		}
		for (Adornment adornment : attrconddef.getAllowedGenAdornments()) {
			adornment.regionFor.feature(ADORNMENT__VALUE).append[noSpace]
		}
	}

	def dispatch void format(CorrVariablePattern corrvariablepattern, extension IFormattableDocument document) {
		corrvariablepattern.surround[newLine]
		corrvariablepattern.surround[indent]
		corrvariablepattern.interior[indent]
		corrvariablepattern.regionFor.feature(CORR_VARIABLE_PATTERN__NAME).surround[noSpace]
		corrvariablepattern.regionFor.feature(CORR_VARIABLE_PATTERN__TYPE).prepend[noSpace]

		corrvariablepattern.regionFor.keyword("-«src»->").prepend[newLine].append[noSpace]
		corrvariablepattern.regionFor.keyword("-«trg»->").prepend[newLine].append[noSpace]
		corrvariablepattern.regionFor.keyword("}").prepend[newLine]
	}

	def dispatch void format(ObjectVariablePattern objectvariablepattern, extension IFormattableDocument document) {
		objectvariablepattern.surround[newLine]
		objectvariablepattern.surround[indent]
		objectvariablepattern.interior[indent]
		objectvariablepattern.regionFor.feature(OBJECT_VARIABLE_PATTERN__NAME).surround[noSpace]
		objectvariablepattern.regionFor.feature(OBJECT_VARIABLE_PATTERN__TYPE).prepend[noSpace]
		
		for (AttributeAssignment assignment : objectvariablepattern.getAttributeAssignments()) {
			assignment.surround[newLine]
			assignment.regionFor.feature(ATTRIBUTE_ASSIGNMENT__OP).surround[noSpace]
		}
		for (AttributeConstraint constraint : objectvariablepattern.getAttributeConstraints()) {
			constraint.surround[newLine]
			constraint.regionFor.feature(ATTRIBUTE_CONSTRAINT__OP).surround[noSpace]
		}
		for (LinkVariablePattern linkVariablePatterns : objectvariablepattern.getLinkVariablePatterns()) {
			format(linkVariablePatterns, document);
		}
	}

	def dispatch void format(LinkVariablePattern linkvariablepattern, extension IFormattableDocument document) {
		linkvariablepattern.surround[newLine]
		
		linkvariablepattern.regionFor.keyword("-").surround[noSpace]
		linkvariablepattern.regionFor.keyword("->").surround[noSpace]
	}
}
